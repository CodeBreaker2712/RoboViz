plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
}

group 'org.magmaoffenburg.roboviz'
version '1.8.5'
mainClassName = 'org.magmaoffenburg.roboviz.MainKt'

application {
    applicationDefaultJvmArgs = ['--add-exports=java.desktop/sun.awt=ALL-UNNAMED']
}

ext {
    jogl_version = '2.4.0'
    log4j_version = '2.20.0'
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            url 'https://www.jogamp.org/deployment/maven/'
        }
    }

    sourceCompatibility = '17'
    targetCompatibility = '17'
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

compileKotlin {
    kotlinOptions.jvmTarget = '17'
}

repositories {
    mavenCentral()
    maven {
        url 'https://www.jogamp.org/deployment/maven/'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'com.github.weisj:darklaf-core:3.0.2'
    implementation "org.jogamp.jogl:jogl-all-main:$jogl_version"
    implementation "org.jogamp.gluegen:gluegen-rt-main:$jogl_version"
    implementation 'org.apache.commons:commons-compress:1.23.0'
    implementation 'org.apache.logging.log4j:log4j-api-kotlin:1.2.0'
    implementation "org.apache.logging.log4j:log4j-api:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-core:$log4j_version"

    implementation project(':jsgl')
}

shadowJar {
    archiveFileName = 'RoboViz.jar'
}
